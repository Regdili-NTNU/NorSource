;;; -*- Mode: tdl; Coding: utf-8; -*-
;;;
;;;              - NorSource -
;;; 
;;;    Copyright (c)  NorSource 1.0
;;;                     
;;;
;;;
;;;    Norwegian University of Science and Technology, 7491 Trondheim
;;;    
;;;
;;;
;;;          irules-v.tdl
;;;       inflectional rules for verbs

%(letter-set (!h klnsfpdgrtu))
%(letter-set (!i kpgtnfdvsl))

%(letter-set (!a abcdefghijklmnopqrstuvwxyzæøå)) ; any letter
%(letter-set (!k bcdfghjklmnpqrstvwxz)) ; not vowel

;;;;;;;;;;;;; VERBS ;;;;;;;;;;;;;;;


;;; DPF addenda

; reduplication with same-named in irules-mal.tdl; but not comment out, according to Elias.
mal-past-inf-like-1_infl_rule := never_unify_lr.
mal-past-inf-like-2_infl_rule := never_unify_lr.


pres-infl_rule :=
%suffix (* r) (s s) (petro petro) (og og)
infl-pres-verb-word &
[ ARGS < [ INFLECTION phon ] > ].

; for English guests
pres-s-infl_rule :=
%suffix (* s)
infl-pres-verb-word &
[ ARGS < [ INFLECTION phon ] > ].


pret-finalstr_infl_rule :=
%suffix (e edde)(a adde)(o odde)(ø ødde)(å ådde)(u udde)(i idde)(æ ædde)(y ydde)
infl-pret-verb-word &
[ ARGS < [ INFLECTION finstr ] > ].


;; Used only for strong verbs. Given the same name as the old rule, to make sure the irregs-files still apply.
;; The %suffix is given to avoid that this rule applies normally, without resorting to the irregs-files.
pret-nonfstr_infl_rule :=
%suffix (donot unify)
infl-pret-verb-word &
[ ARGS < [ INFLECTION nonfstr-strong ] > ].

pret-nonfstr-te_infl_rule := 
%suffix (e te) (ffe fte) (kke kte) (lle lte) (nne nte) (ppe pte) (mme mte) (es tes)(ffes ftes)(de dte)(kkes ktes)(kes kedes)		; Skal vi gjera noko med at dette aksepterer "kallte"
infl-pret-verb-word &
[ ARGS < [ INFLECTION nonfstr-te ] > ].

pret-nonfstr-et_infl_rule :=
%suffix (e a) (e et) (tes ttes) (es edes)
infl-pret-verb-word &
[ ARGS < [ INFLECTION nonfstr-et ] > ].

pret-nonfstr-de_infl_rule := 
%suffix (e de) (gge gde) (es des)
infl-pret-verb-word &
[ ARGS < [ INFLECTION nonfstr-de ] > ].


ppart-finalstr-dd_infl_rule :=
%suffix (o odd)(ø ødd)(å ådd)(u udd)(i idd)(y ydd)(e edd)(a add)
infl-perf-part-verb-word &
[ ARGS < [ INFLECTION finstr-dd ] > ].

ppart-finalstr-tt_infl_rule :=
%suffix (e ett)(a att)(æ ætt)(å ått)(i itt)(y ytt)(ø øtt)(o ott)
infl-perf-part-verb-word &
[ ARGS < [ INFLECTION finstr-tt ] > ].


;; Used only for strong verbs. Given the same name as the old rule, to make sure the irregs-files still apply.
;; The %suffix is given to avoid that this rule applies normally, without resorting to the irregs-files.
ppart-nonfstr_infl_rule := 
%suffix (donot unify)
infl-perf-part-verb-word &
[ ARGS < [ INFLECTION nonfstr-strong ] > ].

ppart-nonfstr-te_infl_rule :=
%suffix (e t) (ffe ft) (kke kt) (nne nt) (lle lt) (ppe pt) (mme mt) (es tes) (ffes ftes)(de dt)(ykkes ykkes)(kkes ktes)(kes kedes)
infl-perf-part-verb-word &
[ ARGS < [ INFLECTION nonfstr-te ] > ].

ppart-nonfstr-et_infl_rule :=
%suffix (e a) (e et) (es es)
infl-perf-part-verb-word &
[ ARGS < [ INFLECTION nonfstr-et ] > ].

ppart-nonfstr-de_infl_rule :=
%suffix (e d) (gge gd) (es des)
infl-perf-part-verb-word &
[ ARGS < [ INFLECTION nonfstr-de ] > ].



inf-const_infl_rule :=
inf-verb-word &
[ ARGS < [ INFLECTION phon ] > ].


imp-const_infl_rule :=
const-imp-verb-word &
[ ARGS < [ INFLECTION finstr ] > ].

#|
; desuspended, for non-PET purposes
imp-delete_infl_rule :=
%suffix (!ae !a)
infl-imp-verb-word &
[ ARGS < [ INFLECTION nonfstr,
	   SYNSEM.LOCAL.CONT.HOOK.INDEX.E.TENSE pres ] > ].
|#

; must be suspended, for PET purposes
;imp-delete_infl_rule :=
;%suffix (!aa !a)(!ae !a)(!ai !a)(!ao !a)(!au !a)(!ay !a)(!aæ !a)(!aø !a)(!aå !a)
;infl-imp-verb-word &
;[ ARGS < [ INFLECTION nonfstr ] > ].

; spørre-spør! gjemme-gjem!

imp-delete_infl_rule :=
%suffix (ae e)(be b)(de d)(ee e)(fe f)(ge g)(ie i)(je j)(ke k)(le l)(ae e)(me m)(mme m)(ne n)(oe o)(pe p)(re r)(spørre spør)(se s)(te t)(ve v)(xe x)
infl-imp-verb-word &
[ ARGS < [ INFLECTION nonfstr,
	   SYNSEM.LOCAL.CONT.HOOK.INDEX.E.TENSE pres ] > ].


s-passive_s_infl_rule := 
%suffix (* s)(s s)(finne fins)
s-pass-word &
[ ARGS < [ INFLECTION phon ] > ].

s-passive_es_infl_rule := 
%suffix (* es)(es es)
s-pass-word & 
  [ ARGS.FIRST.INFLECTION finstr ].


;interjection_infl_rule := 
;interjection-word.

;LH191215 Has to be worked on a lot. For 'Ola ligger og sover' it generates i.a. 'Ola ligger og soving' and 'Ola ligger og ing'
;nominalized_ing_infl_rule :=
;%suffix (* ing)(e ing)
;nominalized-ing-verb-word &
;[ ARGS < [ INFLECTION phon ] > ].
